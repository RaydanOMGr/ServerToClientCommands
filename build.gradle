apply plugin: "java"

version '1.0.1'
def minVersion = 145

sourceCompatibility = JavaVersion.VERSION_16

sourceSets.main.java.srcDirs = ["src"]

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

repositories{
    mavenCentral()
    maven{ url "https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository" }
    maven{ url 'https://www.jitpack.io' }
}

ext{
    //the build number that this plugin is made for
    mindustryVersion = "v${minVersion}"
    jabelVersion = "93fde537c7"
}

//java 8 backwards compatibility flag
allprojects{
    tasks.withType(JavaCompile){
        options.compilerArgs.addAll(['--release', '8'])
    }
}

dependencies{
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
    compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"
    annotationProcessor "com.github.Anuken:jabel:$jabelVersion"
}

processResources {
    def versionMap = [minVersion: minVersion, version: project.version]

    versionMap.each { k, v ->
        inputs.property k, v
    }

    println versionMap

    filesMatching("plugin.json") {
        expand versionMap
    }
}

jar{
    archiveFileName = "${project.archivesBaseName}-${project.version}.jar"
    from{
        configurations.runtimeClasspath.collect{it.isDirectory() ? it : zipTree(it)}
    }
}
